/**
 * 	Autores:fddd
 *	Fecha ultima revision:
 * 	Comentarios:
 */
options
{
  static = true;
  ignore_case = true;
}

PARSER_BEGIN(CompiladorMiniLeng)

import java.io.FileInputStream;
import java.io.FileNotFoundException;

public class CompiladorMiniLeng
{
  public static void main(String args []) throws ParseException
  {
    CompiladorMiniLeng parser = new CompiladorMiniLeng(System.in);
	System.out.println("Leyendo de la entrada estandar...");
    try { 
    	//CompiladorMiniLeng parser = new CompiladorMiniLeng(new FileInputStream(args[0]));
		//System.out.println("Leyendo de la entrada estandar...");
    	CompiladorMiniLeng.programa();
    	System.out.println("Analizado correctamente");
    }
    catch (TokenMgrError e) {
		//Error léxico
		try {
			System.out.println("ERROR LÉXICO ("
			+ SimpleCharStream.getBeginLine()
			+ ", "
			+ SimpleCharStream.getBeginColumn()
			+ "): Símbolo no reconocido: \""
			+ SimpleCharStream.readChar() + "\""
			);
		} catch (Exception ex) {
			// Excepcion por readChar
			System.out.println(ex.getMessage());
		}
	}
	catch (ParseException e) {
		//Error sintáctico
		System.out.println("Error sintáctico!");
        System.out.println(e.getMessage());
        CompiladorMiniLeng.ReInit(System.in);
	}
	/*catch (FileNotFoundException e) {
	  System.out.println("Ha ocurrido un error al intentar leer el fichero: "
	  	+ args[0]);
	  System.out.println(e.getMessage());
	}*/
	catch (Exception e) {
	  System.out.println(e.getMessage());
	}
    catch (Error e) {
        System.out.println("Oops.");
        System.out.println(e.getMessage());
	}
  }

  static void error_sintactico(Token currentToken, int[][] expectedTokenSequences, String[] tokenImage) {
    	String eol = System.getProperty("line.separator", "\n");
      	StringBuffer expected = new StringBuffer();
      	int maxSize = 0;
		for (int i = 0; i < expectedTokenSequences.length; i++) {
      		if (maxSize < expectedTokenSequences[i].length) {
        		maxSize = expectedTokenSequences[i].length;
      		}
      		for (int j = 0; j < expectedTokenSequences[i].length; j++) {
        		expected.append(tokenImage[expectedTokenSequences[i][j]]).append(' ');
      		}
      		if (expectedTokenSequences[i][expectedTokenSequences[i].length - 1] != 0) {
        		expected.append("...");
      		}
      		expected.append(eol).append("    ");
    	}
    	Token t = currentToken.next;
		System.out.println("ERROR SINTÁCTICO ("
		+ currentToken.next.beginLine
		+ ", "
		+ currentToken.next.beginColumn
		+ ") : "
		+ " Se ha encontrado un token "
		+ tokenImage[t.kind]
		+ " "
		+ currentToken.next.image
		+ ", se esperaba: "
		+ expected
		);
		
		//System.out.println(e.toString());
		do {
		t = getNextToken();
		} while (t.kind != tFIN_SENTENCIA && t!=null && t.kind != EOF );
  }
}

PARSER_END(CompiladorMiniLeng)

SKIP :
{
  " "
| < "%%" (~["%"])* "%%">
| <"%" (~["\n", "\r"])*>
| "\r"
| "\t"
| "\n"
}



TOKEN : /* AGRUPACIONES */
{
  <  tLLAVE_IZQ : "{" >
| <  tLLAVE_DCHA : "}" >
}

// ADICION NUESTRA.

TOKEN : /*  */
{
  < tPROGRAMA : "programa">
| < tPRINCIPIO : "principio">
| < tFIN : "fin">
| < tSI: "si" >
| < tENT: "ent" >
| < tSI_NO: "si_no" >
| < tFSI : "fsi">
| < tMQ : "mq" >
| < tFMQ : "fmq" >
}

TOKEN: /* E/S */
{
  < tESCRIBIR : "escribir" >
| < tLEER : "leer" >
}

TOKEN : /* Operador multiplicativo  */
{
  < tAND : "and" >
| < tMOD : "mod" >
| < tOR : "or" >
| < tNOT : "not" >
}

TOKEN : /*tipo_variables */
{
  < tENTERO : "entero" >
| < tBOOLEANO : "booleano" >
| < tCARACTER : "caracter" >
}

TOKEN : /*valor booleano*/
{
  < tTRUE : "true" >
| < tFALSE : "false" >
}

TOKEN : /* conversion de tipo entero y caracter */
{
  < tENTACAR : "entacar" >
| < tCARAENT : "caraent" >
}

TOKEN: /* accion */
{
  < tACCION : "accion" >
}

TOKEN : /* clase_parametros */
{
  < tVAL : "val" >
| < tREF : "ref" >
}

TOKEN : /* operador_relacional */
{
  < tMAYOR : ">" >
| < tMENOR : "<" >
| < tIGUAL : "=" >
| < tMAI : ">=" >
| < tMEI : "<=" >
| < tNI : "<>" >

}

TOKEN : /* otros */
{
  < tOPAS : ":=" >
| < tFIN_SENTENCIA : ";" >
| < tCOMA : "," >
| < tPARENTESIS_IZQ : "(" >
| < tPARENTESIS_DCH : ")" >
}

TOKEN: /* Operadores aritmÃ©ticos */
{
  < tMAS : "+">
| < tMENOS : "-" >
| < tDIV : "/" >
| < tMUL : "*" >
}

TOKEN : /* VALORES */
{
  < #DIGITO : ["0"-"9"] >
| < #LETRA : ["a"-"z","_","0"-"9"] >
| < tCONSTENTERA : (< DIGITO >)+ >
| < tCONSTCHAR : "\"" ~["\""] "\"">
| < tCONSTCAD : "\"" (~["\""])* "\"">
| <  tIDENTIFICADOR : (["a"-"z"] | ("_")+ (["a"-"z","0"-"9"])) (["a"-"z", "0"-"9"] | ( ("_")+ (["a"-"z","0"-"9"]) ) )* >
}

void programa ():
{}
{
	try {
	  <tPROGRAMA> <tIDENTIFICADOR> <tFIN_SENTENCIA> declaracion() declaracion_acciones() bloque_sentencias() < EOF >
	}
    catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
  
}

void declaracion_variables():
{}
{
  	try {
		( declaracion() < tCOMA > )*
	}
    catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void declaracion ():
{}
{
  tipo_variables() identificadores()
}

void identificadores(): // Error sintactico
{}
{
  	try {
  	  <tIDENTIFICADOR> ( "," <tIDENTIFICADOR> )*
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void tipo_variables(): // Error sintactico
{}
{
  	try {
  	  <tENTERO> | <tCARACTER> | <tBOOLEANO>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void declaracion_acciones():
{}
{
  (declaracion_accion())*
}

void declaracion_accion(): // Error sintactico
{}
{
  	try {
  	  cabecera_accion() < tFIN_SENTENCIA > declaracion() declaracion_acciones() bloque_sentencias()
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void cabecera_accion(): // Error sintactico
{}
{
  	try {
  	  <tACCION> <tIDENTIFICADOR> ( parametros_formales() )?
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void parametros_formales() : // Error sintactico
{}
{
  	try {
  	  < tPARENTESIS_IZQ > parametros() < tPARENTESIS_DCH > < tFIN_SENTENCIA>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void lista_parametros(): // Error sintactico
{}
{
  	try {
  	  <tIDENTIFICADOR> ("," <tIDENTIFICADOR>)* 
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void parametros(): // Error sintactico
{}
{
	try {
  	  	  < tVAL > tipo_variables() lista_parametros() ( < tFIN_SENTENCIA >parametros() )?
		| < tREF > tipo_variables() lista_parametros() ( < tFIN_SENTENCIA >parametros() )?
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}



void bloque_sentencias(): // Error sintactico
{}
{
  	
  	try {
  	 	<tPRINCIPIO> lista_sentencias() <tFIN>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void lista_sentencias():
{}
{
  ( sentencia() )+
}

void sentencia(): // Error sintactico
{}
{
	try {
  	    leer() < tFIN_SENTENCIA >
		| escribir() < tFIN_SENTENCIA >
		| <tIDENTIFICADOR> asig_incaccion()
		| seleccion()
		| mientras_que()
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void leer(): // Error sintactico
{}
{
	try {
  	  <tLEER> <tPARENTESIS_IZQ > lista_asignables() < tPARENTESIS_DCH >
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void lista_asignables(): // Error sintactico
{}
{
  	try {
  	  <tIDENTIFICADOR> ("," <tIDENTIFICADOR>)*
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void lista_escribibles():
{}
{
  lista_expresiones()
}

void escribir(): // Error sintactico
{}
{
  	try {
  	  <tESCRIBIR> <tPARENTESIS_IZQ> lista_escribibles() <tPARENTESIS_DCH>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void asig_incaccion():
{}
{
  asignacion() | invocacion_accion()
}

void asignacion(): // Error sintactico
{}
{
  	try {
  	  <tOPAS> expresion() < tFIN_SENTENCIA >
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void invocacion_accion(): // Error sintactico
{}
{
  	try {
  	  argumentos() < tFIN_SENTENCIA >
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void mientras_que(): // Error sintactico
{}
{
  	try {
  	  <tMQ> expresion() lista_sentencias() <tFMQ>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void seleccion(): // Error sintactico
{}
{
  	try {
  	  <tSI> expresion() <tENT> lista_sentencias() ( <tSI_NO> lista_sentencias())? <tFSI>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void argumentos(): // Error sintactico
{}
{
  	try {
  	  < tPARENTESIS_IZQ > ( lista_expresiones() )? < tPARENTESIS_DCH >
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void lista_expresiones(): // Error sintactico
{}
{
  	try {
  	  expresion() (< tCOMA > expresion())*
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void expresion():
{}
{
  expresion_simple() ( operador_relacional() expresion_simple() )?
}

void operador_relacional(): // Error sintactico
{}
{
  	try {
  	  <tIGUAL> | <tNI> | <tMENOR> | <tMAYOR> | <tMEI> | <tMAI>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void expresion_simple(): // Error sintactico
{}
{
  	try {
  	  (< tMAS > | < tMENOS >)? termino() ( operador_aditivo() termino() )*
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void operador_aditivo(): // Error sintactico
{}
{
	try {
	  < tMAS > | < tMENOS > | <tOR>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void termino():
{}
{
  factor() ( operador_multiplicativo() factor() )*
}

void operador_multiplicativo(): // Error sintactico
{}
{
  	try {
  	  < tMUL > | <tDIV> | <tMOD> | <tAND>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}

void factor(): // Error sintactico
{}
{
    try {
      	<tNOT> factor()
    | < tPARENTESIS_IZQ > expresion() < tPARENTESIS_DCH >
    | <tENTACAR> < tPARENTESIS_IZQ > expresion() < tPARENTESIS_DCH >
    | <tCARAENT> < tPARENTESIS_IZQ > expresion() < tPARENTESIS_DCH >
    | <tIDENTIFICADOR>
    | <tCONSTENTERA>
    | <tCONSTCHAR>
    | <tCONSTCAD>
    | <tTRUE>
    | <tFALSE>
  	}
  	catch (ParseException e) {
      	error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);
	}
}
